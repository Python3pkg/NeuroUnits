EQNSET chlstd_leak {
    i = g * (V-erev)

    <=> PARAMETER g
    <=> PARAMETER erev
    <=> OUTPUT    i:(mA/cm2)   METADATA {"mf":{"role":"TRANSMEMBRANECURRENT"} }
    <=> INPUT V: mV            METADATA {"mf":{"role":"MEMBRANEVOLTAGE"} }
}


EQNSET chlstd_hh_k {

    from std.neuro import RateConstant5
    
    #from std.math import exp
    i = g * (v-erev) * n**4

    ninf = n_alpha_rate / (n_alpha_rate + n_beta_rate)
    ntau = 1.0 / (n_alpha_rate + n_beta_rate)
    n' = (ninf-n) / ntau

    #StdFormAB(V,a1,a2,a3,a4,a5) = (a1 + a2*V)/(a3+exp( (V+a4)/a5) )
    
    n_alpha_rate = RateConstant5( V=v,a1=n_a1,a2=n_a2,a3=n_a3,a4=n_a4,a5=n_a5)
    n_beta_rate =  RateConstant5( V=v,a1=n_b1,a2=n_b2,a3=n_b3,a4=n_b4,a5=n_b5)

    <=> PARAMETER g
    <=> PARAMETER erev
    <=> PARAMETER n_a1, n_a2, n_a3, n_a4, n_a5
    <=> PARAMETER n_b1, n_b2, n_b3, n_b4, n_b5
    <=> OUTPUT    i:(mA/cm2)      METADATA {"mf":{"role":"TRANSMEMBRANECURRENT"} }
    <=> INPUT     v: mV           METADATA {"mf":{"role":"MEMBRANEVOLTAGE"} }
}


EQNSET chlstd_hh_na {

    from std.neuro import RateConstant5
    from std.math import exp
    i = g * (v-erev) * m**3*h

    minf = m_alpha_rate / (m_alpha_rate + m_beta_rate)
    mtau = 1.0 / (m_alpha_rate + m_beta_rate)
    m' = (minf-m) / mtau

    hinf = h_alpha_rate / (h_alpha_rate + h_beta_rate)
    htau = 1.0 / (h_alpha_rate + h_beta_rate)
    h' = (hinf-h) / htau

    #StdFormAB(V,a1,a2,a3,a4,a5) = (a1 + a2*V)/(a3+exp( (V+a4)/a5) )
    
    m_alpha_rate = RateConstant5( V=v,a1=m_a1,a2=m_a2,a3=m_a3,a4=m_a4,a5=m_a5)
    m_beta_rate =  RateConstant5( V=v,a1=m_b1,a2=m_b2,a3=m_b3,a4=m_b4,a5=m_b5)
    h_alpha_rate = RateConstant5( V=v,a1=h_a1,a2=h_a2,a3=h_a3,a4=h_a4,a5=h_a5)
    h_beta_rate =  RateConstant5( V=v,a1=h_b1,a2=h_b2,a3=h_b3,a4=h_b4,a5=h_b5)

    <=> PARAMETER g
    <=> PARAMETER erev
    <=> PARAMETER m_a1, m_a2, m_a3, m_a4, m_a5
    <=> PARAMETER m_b1, m_b2, m_b3, m_b4, m_b5
    <=> PARAMETER h_a1, h_a2, h_a3, h_a4, h_a5
    <=> PARAMETER h_b1, h_b2, h_b3, h_b4, h_b5
    <=> OUTPUT    i:(mA/cm2)      METADATA {"mf":{"role":"TRANSMEMBRANECURRENT"} }
    <=> INPUT     v: mV                 METADATA {"mf":{"role":"MEMBRANEVOLTAGE"} }

}
